buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath rootProject.ext.plugin.androidPlugin
        classpath rootProject.ext.plugin.retroLambda
        classpath rootProject.ext.plugin.androidApt
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


Integer VERSION_CODE = 2016011900 //yyyymmddhh
String VERSION_NAME = "1.0.0"
String APK_NAME = "BID"
android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    output.outputFile.name.replace(output.outputFile.name, VERSION_CODE + "-" + VERSION_NAME + "-" + APK_NAME + ".apk"))
        }
    }

    defaultConfig {
        applicationId "org.hispindia.bidtrackerreports"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode VERSION_CODE
        versionName VERSION_NAME
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        dev {
            buildConfigField 'String', 'HOST', '"https://bid.dhis2.org/epireg"'
        }

        staging {
            buildConfigField 'String', 'HOST', '"https://bid.dhis2.org/epireg"'
        }
    }
    // Configure for RetroLambda
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE'
        exclude 'LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }
    configurations {
        all*.exclude module: 'commons-logging'
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    def dependency = rootProject.ext.dependency
    compile project(':..:dhis2-android-sdk:app')
//    compile project(':android-core')

    compile dependency.appcompatv7
    compile dependency.supportDesign
    compile dependency.recyclerviewv7

    compile dependency.dagger
    apt dependency.daggerCompiler
//    provided dependency.generatedAnnotation;
    compile dependency.butterknife
    compile dependency.rxAndroid
    compile dependency.rxJava

    testCompile dependency.jUnit
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }

}

